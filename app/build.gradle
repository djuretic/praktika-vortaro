apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 32
    defaultConfig {
        applicationId 'com.esperantajvortaroj.app'
        minSdkVersion 15
        targetSdkVersion 32
        versionCode 24
        versionName "0.24"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        buildConfigField("int", "VERSION_CODE", "${defaultConfig.versionCode}")
        buildConfigField("String","VERSION_NAME","\"${defaultConfig.versionName}\"")
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
    buildFeatures {
        viewBinding = true
    }
    namespace 'com.esperantajvortaroj.app'
}


preBuild.doFirst {
    def f = new File('./app/src/main/assets/databases/vortaro.db')
    if (!f.exists()) {
        new URL('https://github.com/djuretic/praktika-vortaro-dicts/releases/download/0.14/vortaro.db').withInputStream{ i -> f.withOutputStream{ it << i }}
    }
    assert f.size() > 0
}

afterEvaluate {
    android.sourceSets.all { sourceSet ->
        if (!sourceSet.name.startsWith('test') || !sourceSet.name.startsWith('androidTest')) {
            sourceSet.kotlin.setSrcDirs([])
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // update to 1.5.1 gives "Duplicate class androidx.lifecycle.ViewModelLazy found in modules" error
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
    implementation 'com.readystatesoftware.sqliteasset:sqliteassethelper:2.0.1'
    implementation 'com.github.douglasjunior:android-simple-tooltip:0.2.2'

    implementation 'androidx.room:room-runtime:2.4.3'
    annotationProcessor 'androidx.room:room-compiler:2.4.3'
    kapt 'androidx.room:room-compiler:2.4.3'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
}
